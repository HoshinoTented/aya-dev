open import Arith::Nat
open import Data::Fin
open import Paths

open data Vec (A : Type) (n : Nat) : Type
| A, zero => vnil
| A, suc m => infixr vcons A (Vec A m)

variable A B : Type
variable n m o : Nat

def map (f : Pi A -> B) (xs : Vec A n) : Vec B n
| f, vnil => vnil
| f, x vcons xs => f x vcons map f xs

def overlap infix ++ (xs : Vec A n) (ys : Vec A m) : Vec A (n + m)
| vnil, ys => ys
| xs, vnil => xs
| x vcons xs, ys => x vcons (xs ++ ys)
bind tighter =

def overlap ++-assoc (xs : Vec A n) (ys : Vec A m) (zs : Vec A o)
  : Path (\i => Vec A (+-assoc.at i)) ((xs ++ ys) ++ zs) (xs ++ (ys ++ zs))
| vnil, ys, zs => idp
| xs, vnil, zs => idp
| xs, ys, vnil => idp
-- | x vcons xs, ys, zs => pmap (x vcons) (++-assoc xs ys zs)
-- ^ Scope check and universe level check failed
| x vcons xs, ys, zs => path (\i => x vcons (++-assoc xs ys zs).at i)

def infix !! (l : Vec A n) (i : Fin n) : A
| a vcons l, fzero => a
| a vcons l, fsuc i => l !! i

def fold (f : Pi B A -> B) (init : B) (xs : Vec A n) : B
| f, init, vnil => init
| f, acc, x vcons xs => fold f (f acc x) xs

def last (Vec A (suc n)) : A
| x vcons vnil => x
-- | _ vcons xs => last xs
-- ^ The purpose of #243 is to make this work
| _ vcons (x vcons xs) => last (x vcons xs)

def tail (Vec A (suc n)) : Vec A n
| _ vcons xs => xs
