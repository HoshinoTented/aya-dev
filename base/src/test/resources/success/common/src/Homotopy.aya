open import Paths

open data S1 : Type
| base
| loop I { | 0 => base | 1 => base }

data Seg : Type
| p1 | p2
| seg I { | 0 => p1 | 1 => p2 }

open data Torus : Type
| point
| line1 I { | 0 => point | 1 => point }
| line2 I { | 0 => point | 1 => point }
| face I I { | 0, i => line2 i
             | 1, i => line2 i
             | i, 0 => line1 i
             | i, 1 => line1 i }

def idpath {A : Type} (a : A) (i : I) : A
| a, i => a

def S1-Torus (s1 s2 : S1) : Torus
| base, base => point
| loop i, base => line1 i
| base, loop i => line2 i
| loop i, loop j => face i j

def Torus-S1 (t : Torus) : Sig S1 ** S1
| point => (base, base)
| line1 i => (loop i, base)
| line2 i => (base, loop i)
| face i j => (loop i, loop j)
