open import Paths

public open data Unit | unit

def coe {a b : Type} (eq : a ↑ = b) (x : a) : b
  => ↑ arcoe eq.at x right

-- *** Types

public open struct EventT : Type 1
  | AgentT : Type 0 => Unit

def GetterT => EventT -> Type 0
def SetterT => Type 0 -> EventT -> EventT
def Invariant (GetT : GetterT) (SetT : SetterT)
  => Pi (T : EventT) (E : Type 0) -> E ↑ = GetT (SetT E T)

-- wish these getter/setters could be generated automatically
def GetAgentT (T : EventT) => T.AgentT
def SetAgentT (E : Type 0) (T : EventT) : EventT
  => new EventT { | AgentT => E }
def AgentT-inv : Invariant GetAgentT SetAgentT
  => \T E => ↑ idp

public open struct Event (T : EventT) : Type 0
  | agent : T.AgentT

def Getter (GetT : GetterT) => Pi {T : EventT} (Event T) -> GetT T

def getAgent {T : EventT} (e : Event T) : T.AgentT => e.agent

def Quantifier (E : Type 0) : Type 1
  => (E -> Type 0) -> Type 0

def mkConstQ {E : Type 0} (x : E) : (E -> Type 0) -> Type 0
  => \f => f x

-- verbs are event quantifiers
def Verb (T : EventT) : Type 1
  => (Event T -> Type 0) -> Type 0

def mkVerb {T : EventT} (p : Event T -> Type 0) : Verb T
  => \f => Sig (e : Event T) (p e) ** (f e)

-- VPs (verb phrases) behave just like verbs
def VP (T : EventT) => Verb T

-- bare DPs (determiner phrases) are just entity quantifiers
def BDP (E : Type 0) => Quantifier E

def DP (SetT : SetterT) (E : Type 0)
  => Pi {T : EventT} -> VP (SetT E T) -> VP (SetT E T)

-- thetas transform a BDP into a DP with that thematic role
def Theta (SetT : SetterT) : Type 1
  => Pi {E : Type 0} -> BDP E -> DP SetT E

def mkTheta (GetT : GetterT)
  (get : Getter GetT)
  (SetT : SetterT)
  (inv : Invariant GetT SetT)
  : Theta SetT
  => \{E : Type 0} (q : BDP E) -- from Theta
  => \{T : EventT} (p : VP (SetT E T)) -- from DP
  => \(f : Event (SetT E T) -> Type 0) -- from VP
  => q (\(x : E) => p (\(e : Event (SetT E T))
    => Sig (get e = coe (inv T E) x) ** (f e)))

def agentDef : Theta SetAgentT
  => \{E} => mkTheta GetAgentT (\{T} => getAgent {T}) SetAgentT AgentT-inv {E}

def Sentence => Type 0

def mkSentence {T : EventT} (p : VP T) : Sentence => p (\x => Unit)
