open data Nat | zero | suc Nat
open data Bool | true | false
def not (b : Bool) : Bool
 | true => false
 | false => true
def ifElse (A : Type) (b : Bool) (x y : A) : A
 | A, true, x, y => x
 | A, false, x, y => y
open data TermTy | natT | boolT
def termTy (t : TermTy) : Type 0
 | natT => Nat
 | boolT => Bool
open data Term (n : TermTy) : Type 0
 | natT => nat Nat
 | natT => succ (Term natT)
 | boolT => bool Bool
 | boolT => inv (Term boolT)
 | A => case (Term boolT) (Term A) (Term A)
def normalize (t : TermTy) (x : Term t) : termTy t
 | _, nat n => n
 | _, succ n => suc (normalize natT n)
 | _, bool b => b
 | _, inv b => not (normalize boolT b)
 | t, case b x y => ifElse (termTy t) (normalize boolT b) (normalize t x) (normalize t y)
